// FOS Server
// Author: Тринитротолуол, heX, rifleman17
// Для удобства последующей заскриптовки карты, квесты "Пасти браминов", "Сгубить браминов"
// и "Отнести мясо псам" вынесены в отдельные файлы.

#include "Tla"

#pragma property Critter PrivateServer uint MapKlamathRobotTimeNextSay

#define STR_ROBOT_ITEM_SEE    ( 1 )

import void InitCowboy( Map& map, bool firstTime ) from "KlamCowboy";
import void InitDantonKilling( Map& map, bool firstTime ) from "KlamDantonKilling";
import void ActivateMobScript( Map& map ) from "Mob";
import void _CollectorInit( Critter& cr, bool firstTime ) from "Collector";

void _KlamathGrazInit( Map& map, bool firstTime )
{
    InitCowboy( map, firstTime );
}

void _KlamathTrapInit( Map& map, bool firstTime )
{
    ActivateMobScript( map );
    InitDantonKilling( map, firstTime );
}

// Использование вакцины в квесте на прививку браминов
void _BrahmaInit( Critter& cr, bool firstTime )
{
    cr.SetEvent( CRITTER_EVENT_SMTH_USE_ITEM, "_BrahmaSmthUseItem" );
}

void _BrahmaSmthUseItem( Critter& cr, Critter& fromCr, Item& item, Critter@ onCritter, Item@ onItem, Item@ onScenery )
{
    if( valid( onCritter ) && onCritter.Id == cr.Id && fromCr.IsPlayer() && item.ProtoId == Content::Item::vaccine )
        RunDialog( fromCr, cr, true );
}


void _RobotInit( Critter& cr, bool firstTime )
{
    _CollectorInit( cr, firstTime );
    cr.SetEvent( CRITTER_EVENT_PLANE_BEGIN, "_RobotPlaneBegin" );
}

int _RobotPlaneBegin( Critter& cr, NpcPlane& plane, int reason, Critter@ someCr, Item@ someItem )
{
    if( plane.Type == AI_PLANE_PICK && __FullSecond >= cr.MapKlamathRobotTimeNextSay )
    {
        cr.MapKlamathRobotTimeNextSay = __FullSecond + __TimeMultiplier * 5;
        cr.SayMsg( SAY_NORM_ON_HEAD, TEXTMSG_DLG, DLGSTR( cr.DialogId, 1 ) );
    }
    return PLANE_RUN_GLOBAL;
}
