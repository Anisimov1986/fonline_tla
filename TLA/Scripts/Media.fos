// FOS Server Client
// Author: cvet

// Templates:
// import void PlayMusic(Critter& cr, string& musicName, uint pos, uint repeat) from "Media";
// import void PlayMusic(Map& map, string& musicName, uint pos, uint repeat) from "Media";
// import void PlayVideo(Critter& cr, string& videoName, bool canStop) from "Media";
// import void PlayVideo(Map& map, string& videoName, bool canStop) from "Media";

#include "Tla"

#ifdef __SERVER
void PlayMusic( Critter& cr, string& musicName, uint pos, uint repeat ) // Export
{
    if( cr.IsPlayer() )
        cr.RunClientScript( "DoPlayMusic", int(pos), int(repeat), 0, musicName, null );
}

void PlayMusic( Map& map, string& musicName, uint pos, uint repeat ) // Export
{
    Critter@[] critters;
    map.GetCritters( 0, FIND_ALL | FIND_ONLY_PLAYERS, critters );
    for( uint i = 0, j = critters.length(); i < j; i++ )
        critters[ i ].RunClientScript( "DoPlayMusic", int(pos), int(repeat), 0, musicName, null );
}

void PlayVideo( Critter& cr, string& videoName, bool canStop ) // Export
{
    if( cr.IsPlayer() )
        cr.RunClientScript( "DoPlayVideo", canStop ? 1 : 0, 0, 0, videoName, null );
}

void PlayVideo( Map& map, string& videoName, bool canStop ) // Export
{
    Critter@[] critters;
    map.GetCritters( 0, FIND_ALL | FIND_ONLY_PLAYERS, critters );
    for( uint i = 0, j = critters.length(); i < j; i++ )
        critters[ i ].RunClientScript( "DoPlayVideo", canStop ? 1 : 0, 0, 0, videoName, null );
}
#endif

#ifdef __CLIENT
// Music/Video, see media.fos
void DoPlayMusic( int pos, int repeat, int, string@ musicName, int[] @ )
{
    PlayMusic( musicName, uint( pos ), uint( repeat ) );
}

void DoPlayVideo( int canStop, int, int, string@ videoName, int[] @ )
{
    PlayVideo( videoName, canStop != 0 );
}
#endif
