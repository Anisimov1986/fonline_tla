// FOS Server
// Author: Sufir

#include "Tla"
#include "V13DClawLib"
#include "Merc.fos"

// Инициализация ошейника.
void _InitDclaw( Critter& merc, bool firstTime )
{
    __EventCritterSomeCritterDead.SubscribeToCritter( merc, e_DclawMasterDead );
    __EventCritterDead.SubscribeToCritter( merc, e_DclawDead );
    __EventCritterFinish.SubscribeToCritter( merc, e_DclawFinish );
    merc.KillExperience = 10;
    _MercInit( merc, firstTime );
}

// Смерть смертокогта.
void e_DclawDead( Critter& dclaw, Critter@ killer )
{
    // Закладываем в труп ошейник.
    Item@ collar = dclaw.AddItem( Content::Item::psychic_nullifier, 1 );
    collar.SetScript( "ItemV13Collar@_CollarInit" );
    // Меняем стадию квеста у игроков в группе с "квест взят или самка поймана" на "ошейник потерян".
    Critter@ master = GetMaster( dclaw );
    int[] ChangeStages = { 1, 2 };
    if( valid( master ) )
    {
        SetGroupQuestStage( master, ChangeStages, 3 );
    }
    // Разрешаем лутать труп.
    dclaw.IsNoSteal = false;
    dclaw.IsNoLoot = false;
    // Запускаем событие мерка merc.fos.
    e_MercDead( dclaw, killer );
}

// Деструктор смертокогта.
void e_DclawFinish( Critter& dclaw, bool deleted )
{
    // Меняем стадию квеста у игроков в группе с "квест взят или самка поймана" на "ошейник потерян".
    Critter@ master = GetMaster( dclaw );
    int[] ChangeStages = { 1, 2 };
    if( valid( master ) )
    {
        SetGroupQuestStage( master, ChangeStages, 3 );
    }
    // Запускаем деструктор мерка из merc.fos.
    // e_MercFinish( dclaw, deleted );
}

// Смерть хозяина.
void e_DclawMasterDead( Critter& dclaw, Critter& deadCr, Critter@ killer )
{
    // Если хозяин был убит, меняем стадию квеста у игроков в группе с "квест взят или самка поймана" на "ошейник потерян".
    Critter@ master = GetMaster( dclaw );
    int[] ChangeStages = { 1, 2 };
    if( valid( master ) )
    {
        if( deadCr.Id == master.Id )
        {
            if( GetLeaderId( deadCr ) > 0 )
            {
                if( !Merc2Player( GetCritter( GetLeaderId( deadCr ) ), dclaw, MERC_FOLLOWER_HARMLESS ) )
                    SetGroupQuestStage( master, ChangeStages, 3 );
                dclaw.NpcRole = 1001;
            }
            else
            {
                SetGroupQuestStage( master, ChangeStages, 3 );
            }
        }

    }

    // Запускаем событие смерти криттера из merc.fos.
    e_MercSmthDead( dclaw, deadCr, killer );
}

// Удаляем смертокогта.
void DclawFinish( Critter& dclaw )
{
    DeleteNpc( dclaw );
}
