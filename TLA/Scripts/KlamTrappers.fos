// FOS Server
// Author: Cracker

#include "Tla"

#define ENTIRE_MOVE_EXIT_TRAP     ( 301 )               // Ентайри на который идут трапперы при возвращении домой
#define MIKE_ROLE_ID              ( 30 )                // Роль Майка.
#define BRIAN_ROLE_ID             ( 31 )                // Роль Брайна.

#define MIKE_ENTIRE_HOME          ( 40 )                // Номер энтайри по которому определятся домашняя позиция Майка в Кламате.
#define BRIAN_ENTIRE_HOME         ( 41 )                // Номер энтайри по которому определятся домашняя позиция Брайна в Кламате.
#define EMMA_ENTIRE_START_HOME    ( 29 )                // Номер энтайри на котором стоит Эмма в Кламате.
#define EMMA_ENTIRE_END_HOME      ( 39 )                // Номер энтайри на который уходит Эмма.

#define START_MOVE_TIME           ( 60 * 60 * 24 * 30 ) // через это время трапперы пойдут в Кламат.

import uint GetNearFullSecond( uint16 year, uint16 month, uint16 day, uint16 hour, uint16 minute, uint16 second ) from "Time";

void _Move( Critter& player, int p0, int p1, int p2 )
{
    Critter@[] critters;
    if( player.GetMap().GetCrittersHex( player.HexX, player.HexY, 100, FIND_ALL, critters ) != 0 )
    {
        for( uint i = 0, j = critters.length(); i < j; i++ )
        {
            Critter@ cr = critters[ i ];
            if( cr.NpcRole == MIKE_ROLE_ID || cr.NpcRole == BRIAN_ROLE_ID )
            {
                uint16 hx = 0;
                uint16 hy = 0;
                GetEntireFreeHex( player.GetMap(), ENTIRE_MOVE_EXIT_TRAP, hx, hy );
                cr.HomeMapId = player.GetMap().Id;
                cr.HomeHexX = hx;
                cr.HomeHexY = hy;
                cr.HomeDir = Random( 0, 5 );
            }
        }
    }
}

void t_Transit( Critter& cr, const Item& entire, bool entered, uint8 dir )
{
    if( cr.NpcRole == MIKE_ROLE_ID || cr.NpcRole == BRIAN_ROLE_ID )
    {
        cr.TransitToMap( ( GetLocation( Content::Location::klamath ) ).GetMapByIndex( 0 ).Id, 1 );
        uint16 hx = 0;
        uint16 hy = 0;
        GetEntireFreeHex( GetLocation( Content::Location::klamath ).GetMapByIndex( 0 ), cr.NpcRole == MIKE_ROLE_ID ? ENTIRE_MOVE_EXIT_TRAP : BRIAN_ENTIRE_HOME, hx, hy );
        cr.HomeMapId = cr.GetMap().Id;
        cr.HomeHexX = hx;
        cr.HomeHexY = hy;
        cr.HomeDir = ( cr.NpcRole == MIKE_ROLE_ID ? 1 : 3 );
        cr.DialogId = ( cr.NpcRole == MIKE_ROLE_ID ? Content::Dialog::klam_mike_post_quest : Content::Dialog::klam_bryan_post_quest );
        cr.BagId = 51;
        GetEntireFreeHex( GetLocation( Content::Location::klamath ).GetMapByIndex( 0 ), EMMA_ENTIRE_START_HOME, hx, hy );
        Critter@[] critters;
        if( GetLocation( Content::Location::klamath ).GetMapByIndex( 0 ).GetCrittersHex( hx, hy, 100, FIND_ALL, critters ) != 0 )
        {
            for( uint i = 0, j = critters.length(); i < j; i++ )
            {
                if( critters[ i ].DialogId == Content::Dialog::klam_emma )
                {
                    critters[ i ].DialogId = Content::Dialog::klam_emma_post_quest;
                    GetEntireFreeHex( GetLocation( Content::Location::klamath ).GetMapByIndex( 0 ), EMMA_ENTIRE_END_HOME, hx, hy );
                    critters[ i ].HomeMapId = critters[ i ].GetMap().Id;
                    critters[ i ].HomeHexX = hx;
                    critters[ i ].HomeHexY = hy;
                    critters[ i ].HomeDir = 3;
                }
            }
        }
    }
    return;
}

void t_TransitMike( Critter& cr, const Item& entire, bool entered, uint8 dir )
{
    if( cr.NpcRole == MIKE_ROLE_ID )
    {
        uint16 hx = 0;
        uint16 hy = 0;
        cr.TransitToMap( GetLocation( Content::Location::klamath ).GetMapByIndex( 1 ).Id, 2 );
        GetEntireFreeHex( GetLocation( Content::Location::klamath ).GetMapByIndex( 1 ), MIKE_ENTIRE_HOME, hx, hy );
        cr.HomeMapId = cr.GetMap().Id;
        cr.HomeHexX = hx;
        cr.HomeHexY = hy;
        cr.HomeDir = 1;
    }
}

void r_StartMove( Critter& player, Critter@ npc )
{
    DeferredCall( GAME_SECOND_TO_TICK( START_MOVE_TIME ), Move, player.Id );
}

void Move( uint value )
{
    Critter@ cr = GetCritter( value );
    if( cr !is null )
        _Move( cr, 0, 0, 0 );
}
