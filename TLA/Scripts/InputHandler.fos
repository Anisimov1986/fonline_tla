// FOS Client

#include "Tla"

import void TryExit() from "GuiScreens";
import void NextCursor() from "GuiScreens";
import void SwitchMainItemMode( bool next ) from "Parameters";

void ModuleInit()
{
    AddKeyDownCallback( OnKeyDown );
    AddKeyUpCallback( OnKeyUp );
}

bool OnKeyDown( uint8 key, string@ text )
{
    HandlePressedKeys();
    if( key != DIK_TEXT )
        return HandleHotkeys( key );
    return false;
}

void OnKeyUp( uint8 key )
{
    HandlePressedKeys();
}

// Hotkeys handler
bool HandleHotkeys( int key )
{
    // Effect example, temporary
    if( IsAltDown() && key == DIK_Q )
        SetEffect( EFFECT_FLUSH_MAP, 0, null );
    if( IsAltDown() && key == DIK_W )
        SetEffect( EFFECT_FLUSH_MAP, 0, "Flush_Map_BlackWhite.glsl" );

    // Hotkeys
    bool isHotkey = true;
    bool inputFocused = ( cast< GUITextInput >( GUI_GetFocusedObject() ) !is null || GUI_IsConsoleActive() );
    if( !IsCtrlDown() && !IsAltDown() && !IsShiftDown() )
    {
        switch( key )
        {
        case DIK_F1:
            __HelpInfo = !__HelpInfo;
            break;

        case DIK_F2:
            // if( SaveText( filePath, text ) )
            //    Message( MSG_GAME( STR_LOG_SAVED ) );
            // else
            //    Message( MSG_GAME( STR_LOG_NOT_SAVED ) );
            break;
        case DIK_F3:
            // if( SaveScreenshot( filePath ) )
            //    Message( MSG_GAME( STR_SCREENSHOT_SAVED ) );
            // else
            //    Message( MSG_GAME( STR_SCREENSHOT_NOT_SAVED ) );
            break;

        case DIK_F6:
            __ShowPlayerNames = !__ShowPlayerNames;
            break;
        case DIK_F7:
            if( __DebugInfo )
                __ShowNpcNames = !__ShowNpcNames;
            break;

        case DIK_F8:
            __MouseScroll = !__MouseScroll;
            __ScrollMouseRight = false;
            __ScrollMouseLeft = false;
            __ScrollMouseDown = false;
            __ScrollMouseUp = false;
            break;

        case DIK_F9:
            if( __DebugInfo )
                CustomCall( "SwitchShowTrack" );
            break;
        case DIK_F10:
            if( __DebugInfo )
                CustomCall( "SwitchShowHex" );
            break;

        // Fullscreen
        case DIK_F11:
            CustomCall( "SwitchFullscreen" );
            break;
        // Minimize
        case DIK_F12:
            CustomCall( "MinimizeWindow" );
            break;

        // Mouse wheel emulate
        case DIK_PRIOR:
            MouseClick( __MouseX, __MouseY, MOUSE_BUTTON_WHEEL_UP );
            break;
        case DIK_NEXT:
            MouseClick( __MouseX, __MouseY, MOUSE_BUTTON_WHEEL_DOWN );
            break;

        // Text input switching
        case DIK_TAB:
            GUI_NextTextInput();
            break;

        // Exit
        case DIK_ESCAPE:
            TryExit();
            break;

        default:
            break;
        }

        if( !inputFocused )
        {
            GUIScreen@ screen = GUI_GetActiveScreen();

            // Open screen
            if( key == DIK_C && ( screen.Index == CLIENT_MAIN_SCREEN_GAME || screen.Index == CLIENT_MAIN_SCREEN_GLOBAL_MAP ) )
                ShowScreen( CLIENT_SCREEN_CHARACTER );
            else if( key == DIK_I && ( screen.Index == CLIENT_MAIN_SCREEN_GAME || screen.Index == CLIENT_MAIN_SCREEN_GLOBAL_MAP ) )
                ShowScreen( CLIENT_SCREEN_INVENTORY );
            else if( key == DIK_P && ( screen.Index == CLIENT_MAIN_SCREEN_GAME || screen.Index == CLIENT_MAIN_SCREEN_GLOBAL_MAP ) )
                ShowScreen( CLIENT_SCREEN_PIP_BOY );
            else if( key == DIK_F && ( screen.Index == CLIENT_MAIN_SCREEN_GAME || screen.Index == CLIENT_MAIN_SCREEN_GLOBAL_MAP ) )
                ShowScreen( CLIENT_SCREEN_FIX_BOY );
            else if( key == DIK_O && ( screen.Index == CLIENT_MAIN_SCREEN_GAME || screen.Index == CLIENT_MAIN_SCREEN_GLOBAL_MAP ) )
                ShowScreen( CLIENT_SCREEN_MENU );
            else if( key == DIK_S && screen.Index == CLIENT_MAIN_SCREEN_GAME )
                ShowScreen( CLIENT_SCREEN_SKILLBOX );
            else if( key == DIK_TAB && screen.Index == CLIENT_MAIN_SCREEN_GAME )
                ShowScreen( CLIENT_SCREEN_MINIMAP );
            // Close opened screen
            else if( ( key == DIK_C && screen.Index == CLIENT_SCREEN_CHARACTER ) ||
                     ( key == DIK_I && screen.Index == CLIENT_SCREEN_INVENTORY ) ||
                     ( key == DIK_P && screen.Index == CLIENT_SCREEN_PIP_BOY ) ||
                     ( key == DIK_F && screen.Index == CLIENT_SCREEN_FIX_BOY ) ||
                     ( key == DIK_O && screen.Index == CLIENT_SCREEN_MENU ) ||
                     ( key == DIK_S && screen.Index == CLIENT_SCREEN_SKILLBOX ) ||
                     ( key == DIK_TAB && screen.Index == CLIENT_SCREEN_MINIMAP ) )
            {
                TryExit();
            }
            // Current game time
            else if( key == DIK_SLASH && ( screen.Index == CLIENT_MAIN_SCREEN_GAME || screen.Index == CLIENT_MAIN_SCREEN_GLOBAL_MAP ) )
            {
                string str = "Time: ";
                str += ( __Day < 10 ? "0" : "" ) + __Day + ".";
                str += ( __Month < 10 ? "0" : "" ) + __Month + ".";
                str += __Year + " ";
                str += ( __Hour < 10 ? "0" : "" ) + __Hour + ":";
                str += ( __Minute < 10 ? "0" : "" ) + __Minute + ":";
                str += ( __Second < 10 ? "0" : "" ) + __Second;
                str += " x" + __TimeMultiplier;
                Message( str, FOMB_GAME );
            }
            // Global map centring
            else if( key == DIK_HOME && screen.Index == CLIENT_MAIN_SCREEN_GLOBAL_MAP )
            {
                CustomCall( "GmapHome" );
            }
            // Other
            else if( key == DIK_SPACE && __Singleplayer )
            {
                CustomCall( "SwitchSingleplayerPause" );
            }
            // Main game screen hotkeys
            else if( screen.Index == CLIENT_MAIN_SCREEN_GAME )
            {
                isHotkey = true;

                switch( key )
                {
                case DIK_Q:
                    CustomCall( "SwitchLookBorders" );
                    break;
                case DIK_W:
                    CustomCall( "SwitchShootBorders" );
                    break;

                case DIK_A:
                {
                    Critter@    chosen = GetChosen();
                    const Item@ item = ( chosen !is null ? chosen.GetSlotItem( SLOT_HAND1 ) : null );
                    if( item !is null && item.Type == ItemType::Weapon && ( item.Mode & 0xF ) < MAX_USES )
                        __Cursor = CursorType::UseWeapon;
                    else
                        isHotkey = false;
                }
                break;

                case DIK_G:
                    CustomCall( "TryPickItemOnGround" );
                    break;

                case DIK_T:
                    __ShowGroups = !__ShowGroups;
                    break;

                case DIK_B:
                    CustomCall( "ChangeSlot" );
                    break;
                case DIK_M:
                    NextCursor();
                    break;
                case DIK_N:
                    SwitchMainItemMode( true );
                    break;

                case DIK_COMMA:
                    SetChosenActions( array< uint > = { CHOSEN_DIR, 1 /* CW */, 0, 0, 0, 0, 0 } );
                    break;
                case DIK_PERIOD:
                    SetChosenActions( array< uint > = { CHOSEN_DIR, 0 /* CCW */, 0, 0, 0, 0, 0 } );
                    break;

                case DIK_HOME:
                {
                    Critter@ chosen = GetChosen();
                    if( chosen !is null )
                        MoveScreen( chosen.HexX, chosen.HexY, 100, true );
                }
                break;
                case DIK_SCROLL:
                {
                    Critter@ chosen = GetChosen();
                    if( chosen !is null )
                        LockScreenScroll( chosen, true );
                }
                break;

                case DIK_H:
                    SetChosenActions( array< uint > = { CHOSEN_USE_SKL_ON_CRITTER, uint( CritterProperty::SkillSneak ), 0, 0, 0, 0, 0 } );
                    break;

                // Skills
                case DIK_1:
                    SetChosenActions( array< uint > = { CHOSEN_USE_SKL_ON_CRITTER, uint( CritterProperty::SkillSneak ), 0, 0, 0, 0, 0 } );
                    break;
                case DIK_2:
                {
                    __Cursor = CursorType::UseSkill;
                    __CursorData = uint( CritterProperty::SkillLockpick );
                }
                    break;
                case DIK_3:
                {
                    __Cursor = CursorType::UseSkill;
                    __CursorData = uint( CritterProperty::SkillSteal );
                }
                break;
                case DIK_4:
                {
                    __Cursor = CursorType::UseSkill;
                    __CursorData = uint( CritterProperty::SkillTraps );
                }
                break;
                case DIK_5:
                {
                    __Cursor = CursorType::UseSkill;
                    __CursorData = uint( CritterProperty::SkillFirstAid );
                }
                break;
                case DIK_6:
                {
                    __Cursor = CursorType::UseSkill;
                    __CursorData = uint( CritterProperty::SkillDoctor );
                }
                break;
                case DIK_7:
                {
                    __Cursor = CursorType::UseSkill;
                    __CursorData = uint( CritterProperty::SkillScience );
                }
                break;
                case DIK_8:
                {
                    __Cursor = CursorType::UseSkill;
                    __CursorData = uint( CritterProperty::SkillRepair );
                }
                break;

                // Not an hotkey
                default:
                    isHotkey = false;
                    break;
                }
            }
            else
            {
                isHotkey = false;
            }
        }
        else
        {
            isHotkey = false;
        }
    }
    else
    {
        switch( key )
        {
        case DIK_F6:
            if( __DebugInfo && IsCtrlDown() )
                __ShowCritId = !__ShowCritId;
            break;
        case DIK_F7:
            if( __DebugInfo && IsCtrlDown() )
                __ShowCritId = !__ShowCritId;
            break;
        case DIK_F10:
            if( IsShiftDown() )
                CustomCall( "DumpAtlases" );
            break;

        // Num Pad
        case DIK_EQUALS:
        case DIK_ADD:
            if( !inputFocused )
            {
                if( IsCtrlDown() )
                    __SoundVolume = CLAMP( __SoundVolume + 2, 0, 100 );
                else if( IsShiftDown() )
                    __MusicVolume = CLAMP( __MusicVolume + 2, 0, 100 );
                else if( IsAltDown() && __FixedFPS < 10000 )
                    __FixedFPS++;
            }
            break;
        case DIK_MINUS:
        case DIK_SUBTRACT:
            if( !inputFocused )
            {
                if( IsCtrlDown() )
                    __SoundVolume = CLAMP( __SoundVolume - 2, 0, 100 );
                else if( IsShiftDown() )
                    __MusicVolume = CLAMP( __MusicVolume - 2, 0, 100 );
                else if( IsAltDown() && __FixedFPS > -10000 )
                    __FixedFPS--;
            }
            break;

        // Exit
        case DIK_ESCAPE:
            if( IsShiftDown() )
                __Quit = true;
            break;

        default:
            isHotkey = false;
            break;
        }

        if( key == DIK_RCONTROL || key == DIK_LCONTROL || key == DIK_RCONTROL || key == DIK_LCONTROL )
            CustomCall( "SetCursorPos" );
    }
    return isHotkey;
}

// Pressed keys handler
void HandlePressedKeys()
{
    bool isGameWithoutConsole = ( GUI_GetActiveScreen().Index == CLIENT_MAIN_SCREEN_GAME && !GUI_IsConsoleActive() );
    __MapZooming = ( isGameWithoutConsole && IsKeyPressed( DIK_Z ) );
    __ScrollKeybLeft = ( isGameWithoutConsole && IsKeyPressed( DIK_LEFT ) );
    __ScrollKeybRight = ( isGameWithoutConsole && IsKeyPressed( DIK_RIGHT ) );
    __ScrollKeybUp = ( isGameWithoutConsole && IsKeyPressed( DIK_UP ) );
    __ScrollKeybDown = ( isGameWithoutConsole && IsKeyPressed( DIK_DOWN ) );
}
