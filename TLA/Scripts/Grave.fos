// FOS Server
// Author: cvet
// Script attached to all graves, in protos.
// Graves pids: 344..347, 374..376.

#include "Tla"

import bool SwitchLocker( Item& locker, bool toOpen ) from "Lockers";


void _GraveInit( Item& grave, bool firstTime )
{
    __EventCritterUseSkill.SubscribeToOnItem( grave, _GraveSkill );
    grave.SetEvent( ITEM_EVENT_USE_ON_ME, "_GraveUseOnMe" );
}

bool _GraveSkill( Critter& cr, CritterProperty skill, Critter@, Item@ grave, const Item@ )
{
    if( skill == SKILL_PICK_ON_GROUND || skill == CritterProperty::SkillLockpick )
    {
        if( grave.Opened )
            cr.ShowContainer( null, grave, TRANSFER_HEX_CONT_DOWN );
        else
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_USE_NOTHING );
        return false;
    }
    return true;
}

bool _GraveUseOnMe( Item& grave, Critter& cr, Item@ usedItem )
{
    if( valid( usedItem ) && usedItem.ProtoId == Content::Item::shovel )
    {
        SwitchLocker( grave, !grave.Opened );
        if( grave.Opened )
        {
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_GRAVE_UNCOVERED );
            cr.Karma -= 5;
            cr.KarmaPerkGraveDigger = 1;
        }
        else
        {
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_GRAVE_COVERED );
        }
        return true;
    }
    return false;
}
